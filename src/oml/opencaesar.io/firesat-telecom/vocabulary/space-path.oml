@dc:title "Example Vocabulary"
@dc:creator "Example Company"
@dc:rights "Copyright 2019, by Example Company"
vocabulary <http://opencaesar.io/firesat-telecom/vocabulary/space-path#> as space-path {
	
	extends <http://www.w3.org/2000/01/rdf-schema#> as rdfs
	extends <http://purl.org/dc/elements/1.1/> as dc
	extends <http://imce.jpl.nasa.gov/foundation/base#> as base
	extends <http://imce.jpl.nasa.gov/foundation/analysis#> as analysis
	extends <http://imce.jpl.nasa.gov/foundation/mission#> as mission
	extends <http://bipm.org/jcgm/vim4#> as vim4
	extends <http://opencaesar.io/firesat-telecom/vocabulary/constraint#> as constraint
	extends <http://opencaesar.io/firesat-telecom/vocabulary/downlink#> as downlink
	extends <http://opencaesar.io/firesat-telecom/vocabulary/transmitter#> as transmitter

	uses <http://iso.org/iso-80000-3.1#> as iso-80000-3.1
	
	//
	// Space Path
	//
	
	concept SpacePath < mission:Junction, vim4:Object [
		restricts mission:joins to exactly 2
	]
	
	concept Frequency < downlink:OperatingFrequency
	
	rule setFrequency [
		SpacePath(sp) & mission:joins(sp, if) & mission:presents(c, if)
			& base:contains(d, c) & downlink:DownlinkArrangement(d) & vim4:characterizes(dfq, d) & downlink:OperatingFrequency(dfq)
			& vim4:isAttributedTo(dfv, dfq) & vim4:characterizes(spfq, sp) & Frequency(spfq) -> vim4:isAttributedTo(dfv, spfq)
	]
	
	concept OriginPower < transmitter:EffectiveIsotropicRadiatedPower
	
	rule setOriginPower [
		SpacePath(sp) & mission:joins(sp, if) & mission:presents(ta, if) & transmitter:Antenna(ta)
		    & transmitter:EffectiveIsotropicRadiatedPower(eirp) & vim4:characterizes(eirp, ta) & vim4:isAttributedTo(eirpv, eirp)
		    & vim4:characterizes(spop, sp) & OriginPower(spop) -> vim4:isAttributedTo(eirp, spop)
	]
	
	concept Length < vim4:IndividualUnitaryQuantity [
		restricts vim4:instantiates to iso-80000-3.1:length-of-path
		restricts all vim4:characterizes to SpacePath
	]
	
	concept SpaceLoss < vim4:IndividualUnitaryQuantity [
		restricts all vim4:characterizes to SpacePath
	]
	
	concept PowerFluxDensity < vim4:IndividualUnitaryQuantity [
		restricts all vim4:characterizes to SpacePath
	]
	
	@rdfs:label "L_s"
	concept SpaceLossCausalConstraint < constraint:CausalConstraint [
		restricts analysis:explains to exactly 1 SpaceLoss
		restricts analysis:analyzes to exactly 2
		restricts analyzesLength to exactly 1 Length
		restricts analyzesFrequency to exactly 1 Frequency
	]
	
	relation explainsSpaceLoss < constraint:explainsQuantity
	@rdfs:label "S"
	relation analyzesLength < constraint:analyzesQuantity
	@rdfs:label "f"
	relation analyzesFrequency < constraint:analyzesQuantity
	
	@rdfs:label "W_f"
	concept PowerFluxDensityConstraint < constraint:CausalConstraint [
		restricts analysis:explains to exactly 1 PowerFluxDensity
		restricts analysis:analyzes to exactly 2
		restricts analyzesOriginPower to exactly 1 Length
		restricts analyzesSpaceLoss to exactly 1 Frequency
	]
	
	relation explainsPowerFluxDensity < constraint:explainsQuantity
	@rdfs:label "P_o"
	relation analyzesOriginPower < constraint:analyzesQuantity
	@rdfs:label "L_s"
	relation analyzesSpaceLoss < constraint:analyzesQuantity
	
}
